group 'tv.uob'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

def grpcVersion = '1.7.0' // CURRENT_GRPC_VERSION

dependencies {
    compile group: "com.google.api.graphql", name: "rejoiner", version: "+"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.google.guava:guava:24.1-jre'
    compile "com.google.api.grpc:proto-google-common-protos:0.1.9"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile 'net.javacrumbs.future-converter:future-converter-java8-guava:1.1.0'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: "9.3.8.v20160314"
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.8.v20160314'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.2'
    compile 'com.google.inject.extensions:guice-servlet:4.2.0'
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir "${projectDir.path}/../../proto"
            // In addition to the default '**/*.proto' (use with caution).
            // Using an extension other than 'proto' is NOT recommended,
            // because when proto files are published along with class files, we can
            // only tell the type of a file from its extension.
            include '**/*.protodevel'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}


apply plugin: 'idea'

idea {
    module {
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
    }
}
jar {
    manifest {
        attributes "Main-Class": "tv.uob.graphql.api.server.GraphQLServer"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
apply plugin: 'application'

startScripts.enabled = false